#% text_encoding = iso8859_1
#
# Copyright © 2007 iFactor Consulting, Inc. (http://www.ifactorconsulting.com)
#
# This software is released under the terms of version 2 of the
# GNU General Public License.  The full license text is
# provided in the license.txt file in the Dialog Designer product
# directory, and is also available at http://www.fsf.org.
#
##########################################################################################
##
## @author Graham Garlick
##
## MENUBAR_ELEMENT - object to hold menubar element data.
##
##########################################################################################
_package user
$
_pragma(classify_level=restricted, topic={dialog_designer})
def_slotted_exemplar(:menubar_element,
		     {
			     {:name,_unset,:writable},
			     {:type,_unset,:writable},
			     {:control_type,_unset,:writable},
			     {:action_message,_unset,:writable}
			     
		     },:dd_element) 
$

_pragma(classify_level=restricted, topic={dialog_designer})
menubar_element.define_shared_constant(
	:control_types,
	{ :button_item, 
	  :toggle_item 
	},
	:public)
$

_pragma(classify_level=restricted, topic={dialog_designer})
menubar_element.define_shared_constant(
	:dd_properties,
	property_list.new_with(
		:name,:string,
		:control_type,menubar_element.control_types,
		:action_message,:string,
		:type,:string),
	:public)
$


_pragma(classify_level=restricted, topic={dialog_designer})
_method menubar_element.init(vals)
	## 
	## Initialise SELF from the property list VALS.

	.name << vals[:name]
	.type << vals[:type]
	.action_message << vals[:action_message]
	.control_type << vals[:control_type].default("button_item")
	
	>> _self 
_endmethod
$

_pragma(classify_level=restricted, topic={dialog_designer})
_method menubar_element.dd_properties()
	## 
	## Return the editable properties for SELF.

	props << _super.dd_properties()

	_for k,e _over menubar_element.dd_properties.fast_keys_and_elements()
	_loop
		props[k] << e
	_endloop

	>> props
_endmethod
$

_pragma(classify_level=restricted, topic={dialog_designer})
_method menubar_element.dialog_description()
	## 
	## Return descriptive text for SELF.

	>> .name
_endmethod
$

_pragma(classify_level=restricted, topic={dialog_designer})
_method menubar_element.dialog_element_id
	## 
	## Return the :name slot value.
	>> .name
_endmethod
$
