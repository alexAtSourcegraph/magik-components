#% text_encoding = cp1252
########################################################################
##
## Last Saved Time-stamp: <2007-09-08 22:14:22 brad.sileo>
##
## Created By: Brad Sileo
## Date: 07-18-2005
## Copyright 2005 Brad Sileo
##
## This file is part of Magik Components for SWAF.
##
## Magik Components for SWAF is free software; you can redistribute it and/or modify
## modify it under the terms of the GNU Lesser General Public
## License as published by the Free Software Foundation; either
## version 2.1 of the License, or (at your option) any later version.

## This library is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
## Lesser General Public License for more details.

## You should have received a copy of the GNU Lesser General Public
## License along with this library; if not, write to the Free Software
## Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
##
########################################################################
_package mclib


_pragma(classify_level=advanced, usage={redefinable})
_method plugin_framework.mclib_df!referenced_resources(_optional context)
	## Returns any resources referenced by me as disply_trees 
	## 
	##

	# (07-20-2006 Brad Sileo): created.
	res << _super.mclib_df!referenced_resources(context)
	file_name   << _self.config_definition_file_name
	module_name << _self.config_definition_module_name.default( _self.module_name )
	_if file_name _isnt _unset
	_then
		_try _with cond
			f << smallworld_product.get_data_file( file_name, module_name )
		_when resource_not_found_error
			_return
		_endtry
	_endif
	res.add_last(dt << display_tree.new(:config_file,f))
	dt.styled_string << styled_string.new(:image,{:bookmark,:ui_resources},
					      "Config File=",
					      :hyperlink,f)
	last << dt
	_for name,fname,ename _over _self.mclib_df!definition_file_base_items(:config_file,f,"Config File=")
	_loop
		a_child << display_tree.new(name,fname)
		a_child.styled_string << styled_string.new(:image,{:bookmark,:ui_resources},
					      ename.default(name),
					      :hyperlink,fname)
		a_child.parent << last
		last << a_child
	_endloop
	>> res
_endmethod
$

_pragma(classify_level=restricted, usage={redefinable})
_iter _method plugin_framework.mclib_df!definition_file_base_items(name, file, external_name)
	## 
	## 
	##

	# (07-29-2006 Brad Sileo): created.
	_if file _is _unset 
	_then
		_return 
	_endif
	config << simple_xml.read_element_file(file )
	_if ( base << config.attribute(:base)) _isnt _unset
	_then		
		( module_name, file_name, extension ) << ( _scatter base.split_by(%.))
		_try _with cond
			f << smallworld_product.get_data_file( file_name + ".xml", module_name )
		_when resource_not_found_error
			write("Not found")
			_return
		_endtry
		_loopbody(name,f,external_name)
		_for name,fname,ename _over _self.mclib_df!definition_file_base_items(name,f,external_name)
		_loop
			_loopbody(name,fname,external_name)
		_endloop
	_endif 
_endmethod
$
