#% text_encoding = iso8859_1
_package user
# Copyright © 2005 Minto Spatial Solutions Ltd.
#
# This software is released under the terms of version 2 of the
# GNU General Public License.  The full license text is
# provided in the license.txt file in the mss product
# directory, and is also available at http://www.fsf.org.

_pragma(classify_level=basic, topic={mss_cu})
##
## mss_cu_config_fw_pi
##
## Plugin devoted to embedding an mss_cu_config_framework within a
## main application framework.
##

def_slotted_exemplar(:mss_cu_config_fw_pi,
{
},
{:plugin})
$

_pragma(classify_level=basic, topic={mss_cu})
## Name of the gui definition file.  Defaults to "gui.xml".
mss_cu_config_fw_pi.define_property(
	:gui_definition_file_name,
	:string,
	"Name of the gui definition file",
	_false, _false, "gui.xml")
$

_pragma(classify_level=basic, topic={mss_cu})
## Name of the module in which the gui definition is located.
mss_cu_config_fw_pi.define_property(
	:gui_definition_module_name,
	:string,
	"Name of module in which gui definition is located")
$

_pragma(classify_level=basic, topic={mss_cu})
## Name of the config definition file.  Defaults to "config.xml".
mss_cu_config_fw_pi.define_property(
	:config_definition_file_name,
	:string,
	"Name of the config definition file",
	_false, _false, "config.xml")
$

_pragma(classify_level=basic, topic={mss_cu})
## Name of the module in which the config definition is located.
mss_cu_config_fw_pi.define_property(
	:config_definition_module_name,
	:string,
	"Name of module in which config definition is located")
$

_pragma(classify_level=basic, topic={mss_cu})
_method mss_cu_config_fw_pi.gui_definition_module_name
	## 
	## Use property (if defined), otherwise just use module name.
	##

	_return _self.property(:gui_definition_module_name).default(_self.module_name)
	
_endmethod
$

_pragma(classify_level=basic, topic={mss_cu})
_method mss_cu_config_fw_pi.config_definition_module_name
	## 
	## Use property (if defined), otherwise just use module name.
	##

	_return _self.property(:config_definition_module_name).default(_self.module_name)
	
_endmethod
$

_pragma(classify_level=restricted, topic={mss_cu})
_method mss_cu_config_fw_pi.init_actions()
	## 
	## Create an action for activating the framework.
	##
	
	_self.add_action(sw_action.new(:activate_mss_cu_config_framework,
				       :short_help_text, _self.message(:activate_mss_cu_config_framework_sh),
				       :engine, _self,
				       :action_message, :|activate_mss_cu_config_framework()|))
	
_endmethod
$

_pragma(classify_level=restricted, topic={mss_cu})
_method mss_cu_config_fw_pi.activate_mss_cu_config_framework()
	## 
	## Activate the framework.
	##

	_if (m << _self.get_dialog(:mss_cu_config_framework)) _is _unset
	_then
		m << mss_cu_config_framework.new(
			     :mss_cu_config_framework, _unset,
			     :config_definition_module_name, _self.config_definition_module_name,
			     :config_definition_file_name, _self.config_definition_file_name,
			     :gui_definition_module_name, _self.gui_definition_module_name,
			     :gui_definition_file_name, _self.gui_definition_file_name)
			     
		_self.cache_dialog(:mss_cu_config_framework, m)
		m.activate_relative_to(_unset, m.message(:title), _self.top_frame, 50, 50)
	_else
		m.activate(_unset, m.message(:title))
	_endif
	m.top_frame.minimized? << _false
	
_endmethod
$
